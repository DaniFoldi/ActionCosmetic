plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group 'com.danifoldi'
version '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

repositories {
    mavenCentral()
    google()
    maven { url 'https://repo.danifoldi.com/repository/maven-releases/'}
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://jitpack.io/' }
    maven { url 'https://repo.dmulloy2.net/repository/public/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.7.0'
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.10.3'

    implementation 'com.danifoldi:DML:0.0.4'
    compileOnly 'com.danifoldi:DataVerse:0.1.0'
    implementation 'com.danifoldi:messagelib:1.0.1'
    implementation 'grapefruit:grapefruit:1.3.0'
    implementation ('com.electronwill.night-config:yaml:3.6.4') {
        exclude module: 'snakeyaml'
    }

    compileOnly 'org.jetbrains:annotations:22.0.0'
    implementation 'javax.inject:javax.inject:1'
    implementation 'com.google.dagger:dagger:2.40.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.40.3'
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    from(sourceSets.main.resources) {
        expand version: project.version
    }
}

shadowJar {
    minimize {
        exclude(dependency('com.electronwill.night-config:.*:.*'))
    }

    relocate 'grapefruit.command', 'com.danifoldi.actioncosmetic.lib.grapefruit'
    relocate 'com.danifoldi.messagelib', 'com.danifoldi.actioncosmetic.lib.messagelib'
    relocate 'com.danifoldi.DML', 'com.danifoldi.actioncosmetic.lib.DML'
    relocate 'javax.inject', 'com.danifoldi.actioncosmetic.lib.inject'
    relocate 'dagger', 'com.danifoldi.actioncosmetic.lib.dagger'
    relocate 'com.electronwill.nightconfig', 'com.danifoldi.actioncosmetic.lib.nightconfig'

    archiveFileName.set("${project.name}-${project.version}.jar")
}